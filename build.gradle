plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.23.7'
}

version '0.1.0'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

test{
    useJUnitPlatform()

    moduleOptions{
        runOnClasspath = true
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.jfoenix:jfoenix:9.0.10'
    implementation 'org.apache.logging.log4j:log4j-core:2.11.1'
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
}

javafx{
    version = '16'
    modules = ['javafx.controls', 'javafx.fxml']
}

run {
    jvmArgs = [
            "--add-exports=javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix",
            "--add-exports=javafx.controls/com.sun.javafx.scene.control=com.jfoenix",
            "--add-exports=javafx.base/com.sun.javafx.binding=com.jfoenix",
            "--add-exports=javafx.graphics/com.sun.javafx.stage=com.jfoenix",
            "--add-exports=javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix",
            "--add-exports=javafx.base/com.sun.javafx.event=com.jfoenix",
            "--add-exports=javafx.graphics/com.sun.javafx.scene=com.jfoenix",
            "--add-opens=java.base/java.lang.reflect=com.jfoenix"
    ]
}

application{
    mainModule = 'warriorSim.Classic'
    mainClass = 'sim.Main'
}

jlink{
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'WarriorSim-Classic'

        jvmArgs = [
                "--add-exports=javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix",
                "--add-exports=javafx.controls/com.sun.javafx.scene.control=com.jfoenix",
                "--add-exports=javafx.base/com.sun.javafx.binding=com.jfoenix",
                "--add-exports=javafx.graphics/com.sun.javafx.stage=com.jfoenix",
                "--add-exports=javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix",
                "--add-exports=javafx.base/com.sun.javafx.event=com.jfoenix",
                "--add-exports=javafx.graphics/com.sun.javafx.scene=com.jfoenix",
                "--add-opens=java.base/java.lang.reflect=com.jfoenix",
                "-Dlog4j.configurationFile=log4j2.xml"
        ]
    }

    forceMerge('log4j-api')

    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/images/app/warrior-wow-icon.$imgType"]

        if(currentOs.windows) {
            installerOptions += ['--win-dir-chooser', '--win-shortcut']
        }
        else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'warriorsim-classic', '--linux-shortcut']
        }
        else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'WarriorSim-Classic']
        }
    }
}
